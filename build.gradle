apply plugin: "java"
apply plugin: "eclipse"
apply plugin: 'org.hidetake.ssh'

buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies { classpath 'org.hidetake:gradle-ssh-plugin:1.1.3' }
}

allprojects {
	apply plugin: "eclipse"
	apply plugin: "idea"
	apply plugin: "java"
	apply plugin: "maven"
	
	sourceCompatibility = 1.7
	[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

	sourceSets.main.java.srcDirs = ["src/main/java/"]
	sourceSets.test.java.srcDirs = ["src/test/java/"]
	sourceSets.main.resources.srcDirs = ["src/main/resources/"]
	sourceSets.test.resources.srcDirs = ["src/test/resources/"]

	version = '1.0-SNAPSHOT'
	group = 'com.github.nog54'
	ext{ appName = 'jmatcher' }

	repositories {
		mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
		maven { url "https://oss.sonatype.org/content/repositories/releases/" }
		jcenter()
	}
}

project(":common") { archivesBaseName = appName + '-common' }

project(":server") {
	archivesBaseName = appName + '-server'

	ext {
		commonsDaemonVersion = '1.0.15'
		log4jVersion = '2.4.1'
		projectName = 'jmatcher'
		artifactName = jar.baseName + '-' + version + '.jar'
	}

	dependencies {
		compile project(":common")
		compile "commons-daemon:commons-daemon:$commonsDaemonVersion"
		compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
		compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
		testCompile "junit:junit:+"
		testCompile "org.exparity:hamcrest-date:1.0.1"
		testCompile "com.googlecode.jmockit:jmockit:1.4"
	}
}

project(":client") {
	archivesBaseName = appName + '-client'
	dependencies { compile project(":common") }
}